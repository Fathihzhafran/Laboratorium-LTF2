/* Program : Tampilan Plotter 
 * Contoh memrogram:   
 *  - oled grafik 
 *  - serial plotter 
 *  - membuat buffer geser 
 *  - operasi bitwise 
 * (c) Eko M. Budi, 2023 
 */ 
 
#define BAUD 500000  // Kecepatan komunikasi serial
 
// Pustaka OLED
#include <Adafruit_GFX.h> 
#include <Adafruit_SSD1306.h> 
 
// Definisi parameter OLED
#define D_WIDTH   128   // Lebar layar OLED
#define D_HEIGHT  32    // Tinggi layar OLED
#define D_RESET   4     // Pin reset OLED
#define D_ADDR    0x3C  // Alamat I2C OLED
Adafruit_SSD1306 oled(D_WIDTH, D_HEIGHT, &Wire, D_RESET); 
 
// Konstanta untuk buffer dan ukuran tampilan
#define N_BYTE 32  // Jumlah byte terakhir yang ditampilkan
#define B_HEIGHT (D_HEIGHT/8) // Tinggi setiap bit
#define B_WIDTH (D_WIDTH/N_BYTE) // Lebar setiap kolom
 
// Fungsi untuk menampilkan data dalam bentuk grafik biner di OLED
void oledPlotBiner(byte d) { 
  static byte d_buffer[N_BYTE]; // Buffer untuk menyimpan data terbaru
 
  // Geser buffer ke kiri untuk memberi ruang bagi data terbaru
  for (int i=0; i<N_BYTE-1; i++) { 
    d_buffer[i] = d_buffer[i+1]; 
  } 
  // Masukkan data terbaru ke buffer
  d_buffer[N_BYTE-1] = d; 
  
  oled.clearDisplay(); // Menghapus tampilan OLED
 
  // Mulai plot data dalam buffer
  for (int j=0; j<N_BYTE-1; j++) { 
    int d0 = d_buffer[j]; 
    int x = j*B_WIDTH;    // Posisi x grafik
    
    // Plot semua bit dari data
    for (int i=0; i<8; i++) {       
      int y = i*B_HEIGHT; // Posisi y grafik
      int h = (d0 & 1); // Ambil bit paling rendah (LSB)
      d0 = d0 >> 1; // Geser bit untuk membaca bit berikutnya
       
      // Sesuaikan posisi bit dengan koordinat OLED
      if (h) { 
        h = B_HEIGHT-1;                 
      } else { 
        y = y + B_HEIGHT-1; 
        h = 1;         
      } 
      
      // Gambar grafik biner
      oled.fillRect(x, y, B_WIDTH, h, SSD1306_WHITE); 
    } 
  } 
  oled.display(); // Tampilkan hasil di OLED
} 
 
// Fungsi untuk menampilkan data biner di Serial Plotter
void plotBiner(byte d) { 
  const int ROW_H = 2; // Jarak antar bit di Serial Plotter
 
  // Plot 8 bit dari bit-7 hingga bit-0
  for(int i=0; i<8; i++) { 
    int h = (d & 0x80) >> 7; // Ambil bit tertinggi (MSB)
    d = d << 1;  // Geser bit ke kiri untuk membaca bit berikutnya
    Serial.print(h + (i*ROW_H));  
    Serial.print(' '); 
  } 
  Serial.println(); // Pindah ke baris baru
} 
 
// Variabel global sebagai penghitung
byte counter=0; 
 
void setup(void) 
{ 
  Serial.begin(BAUD); // Inisialisasi komunikasi serial
  Serial.println("\nPlotter Test"); 
 
  // Inisialisasi OLED
  if (!oled.begin(SSD1306_SWITCHCAPVCC, D_ADDR)) { 
    Serial.println("OLED allocation failed"); 
    for (;;); // Jika gagal, hentikan program
  } 
 
  // Header untuk Serial Plotter
  Serial.println("d7 d6 d5 d4 d3 d2 d1 d0");   
}

void loop(void) 
{ 
  counter++; // Tambah nilai counter
  oledPlotBiner(counter); // Tampilkan grafik biner di OLED
  plotBiner(counter); // Kirim data biner ke Serial Plotter
  delay(100); // Tunggu 100 ms sebelum iterasi berikutnya
}
